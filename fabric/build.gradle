plugins {
    id "java"
    id "idea"
    id "maven-publish"
    id "fabric-loom"
    id "me.shedaniel.unified-publishing"
}

base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation libs.fabric.loader
    modImplementation libs.fabric.api
    implementation project(":common")
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }

        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

if (System.getenv("CURSEFORGE_API_TOKEN") != null && System.getenv("MODRINTH_TOKEN") != null) {
    unifiedPublishing {
        project {
            displayName = "V${version} (Fabric ${project.minecraft_version})"
            changelog = rootProject.file("CHANGELOG.md").text
            releaseType = version.toString().contains("beta") ? "beta" : "release"
            gameVersions = [project.minecraft_version]
            gameLoaders = ["fabric", "quilt"]

            mainPublication tasks.remapJar

            relations {
                depends {
                    curseforge = "fabric-api"
                    modrinth = "fabric-api"
                }
            }
//
//            curseforge {
//                token = System.getenv("CURSEFORGE_API_TOKEN")
//                id = "000000"
//            }

            modrinth {
                token = System.getenv("MODRINTH_TOKEN")
                id = "P7dR8mSH"
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}

tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}
